Prog. Program ::= "module" BIdent "where" "{" [Declare] "}";

DLet. Declare ::= BIdent [Arg] "=" Exp ;
DImport. Declare ::= "use" BIdent ;
DImportSubst. Declare ::= "use" BIdent "{" [ImpSubstLine] "}";
DType. Declare ::= BIdent ":" Type ;
DSinkConn.Declare ::= Exp "*>" String ;
DReadSrc.Declare ::= BIdent "<*" String ;
DStage.Declare ::= "stage" BIdent Integer;
DStageNeg.Declare ::= "stage" BIdent "-" Integer;

Add. Exp3 ::= Exp2 "+" Exp3 ;
Sub. Exp3 ::= Exp2 "-" Exp3 ;
Mul. Exp4 ::= Exp3 "*" Exp4 ;
Div. Exp4 ::= Exp3 "/" Exp4 ;
Negate. Exp5 ::= "-" Exp4;
Natexp. Exp5 ::= "exp" Exp ;
Natlog. Exp5 ::= "ln" Exp ;
Realpart. Exp5 ::= "re" Exp ;
Imagpart. Exp5 ::= "im" Exp ;

And. Exp1 ::= Exp2 "&&" Exp2 ;
Or. Exp1 ::= Exp2 "||" Exp2 ;
Not. Exp2 ::= "!" Exp3 ;

ECmp.Exp1 ::= Exp2 CmpOp Exp2 ;

If. Exp ::= "if" Exp1 "then" Exp1 "else" Exp1 ;

Lam. Exp ::= "\\" Pat "->" Exp ;
App. Exp5 ::= Exp Exp ;


Pair. Exp4 ::= "(" Exp "," Exp ")" ;
Pair3. Exp4 ::= "(" Exp "," Exp "," Exp ")" ;
Nil. Exp6 ::= "[]" ;
Cons. Exp ::= Exp "::" Exp ;
ListLit.Exp4 ::= "[" [Exp] "]" ;

Var. Exp6 ::= BIdent ;
EConst. Exp6 ::= Const ;

Sig. Exp1 ::= "{:" Exp ":}" ;
SigVal. Exp6 ::= "<:" Exp ":>" ;
SigAt. Exp3 ::= Exp1 "@" Exp1 ;
SigDelay. Exp3 ::= "delay" Exp Exp  ;

Event. Exp4 ::= "[:" Exp ":]" ;
Switch. Exp ::= "switch" "{" Exp ";" [SwitchLine] "}";
Box. Exp1 ::= "box" Exp1;
Translate. Exp1 ::= "translate" Exp1 Exp1;
Colour. Exp1 ::= "colour" Exp1 Exp1;

ELet.Exp ::= "let" "{" [LetLine] "}" "in" Exp ;
ECase.Exp ::= "case" Exp "of" "{" [CaseLine] "}"  ;

SwitchLine. SwitchLine ::= Exp "~>" Exp ;
LetLine. LetLine ::= BIdent "=" Exp ;
CaseLine. CaseLine ::= Pat "->" Exp ;
Arg. Arg ::= Pat1 ;

ImpSubstLine. ImpSubstLine ::= BIdent "=>" Exp ; 

PVar. Pat1 ::= BIdent ;
PWild. Pat1 ::= "_" ;
PLit. Pat1 ::= Const ;
PPair. Pat1 ::= "(" Pat "," Pat ")" ;
PNil. Pat1 ::= "[]" ;
PCons. Pat ::= Pat "::" Pat ;

CInt. Const ::= Integer ;
CDbl. Const ::= Double ;
CUnit. Const ::= "()" ;
CTrue. Const ::= "true" ;
CFalse. Const ::= "false" ;

TUnit. Type ::= "()" ;
TLam. Type ::= Type "->" Type ;
TPair2. Type ::= "(" Type "," Type ")" ;
TPair3. Type ::= "(" Type "," Type "," Type ")" ;


Lt. CmpOp ::= "<" ;
Gt. CmpOp ::= ">" ;
Le. CmpOp ::= "<=" ;
Ge. CmpOp ::= ">=" ;
Eq. CmpOp ::= "==" ;
Ne. CmpOp ::= "!=" ;

coercions Exp 6;
coercions Pat 1;
--coercions Const 0 ;

separator Declare ";"  ;
separator SwitchLine ";"  ;
separator LetLine ";"  ;
separator CaseLine ";"  ;

separator Exp ","  ;

separator BIdent " " ;

separator Arg "" ;
separator ImpSubstLine "," ;
comment "--" ;

comment "{-" "-}" ;

layout "where", "of", "let", "switch";
layout stop "in";

--layout toplevel ;

token BIdent ((letter|'_') (letter|digit|'_'|'\'')*) ;
